plugins {
    id "com.zoltu.kotlin" version "1.0.5"
    id "com.qixalite.spongestart" version "1.5.2"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

group "de.randombyte"
version "v2.0"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
}

spongestart {
    eula true
    spongeVanillaBuild "1.10.2-5.1.0-BETA-366"
}

ext.spongeApiVersion = "5.0.0"
ext.kotlinVersion = "1.0.5"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.spongepowered:spongeapi:$spongeApiVersion"
    kapt "org.spongepowered:spongeapi:$spongeApiVersion"
    compile "com.github.randombyte-developer.kosp:kosp:v0.2.5"
}

shadowJar {
    dependencies {
        include dependency("org.jetbrains.kotlin:kotlin-runtime")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib")
        include dependency("com.github.randombyte-developer.kosp:kosp")
    }

    exclude "StartServer*.*"

    relocate "kotlin", "de.randombyte.holograms.internal.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.holograms.internal.kosp"

    classifier = null // Remove '-all' suffix from output file name
}