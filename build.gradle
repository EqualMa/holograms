buildscript {
    ext.kotlinVersion = "1.1.1-eap-26"

    repositories {
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "flavor.pie.promptsign" version "1.0.2"
}

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"

group "de.randombyte"
version "2.3"

allprojects {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
        maven { url "https://repo.spongepowered.org/maven/" }
        maven { url "https://jitpack.io" }
    }
}

ext.spongeApiVersion = "7.0.0-SNAPSHOT"

configurations {
    compile.extendsFrom kapt
    compile.extendsFrom shadow
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    kapt "org.spongepowered:spongeapi:$spongeApiVersion"
    shadow("com.github.randombyte-developer.kosp:kosp:v0.3.11.2") {
        transitive = false
    }
    shadow(project("holograms-api")) {
        transitive = false
    }
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.holograms.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.holograms.shaded.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar
signArchives.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}