buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "gradle.plugin.flavor.pie:promptsign:1.0.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

def isJitpack = System.getenv("JITPACK") != null

if (!isJitpack) {
    apply plugin: "flavor.pie.promptsign"
}

group "de.randombyte"
version "3.1.6"

allprojects {
    repositories {
        jcenter()
        maven { url "https://repo.spongepowered.org/maven/" }
        maven { url "https://jitpack.io" }
        maven { url "http://repo.bstats.org/content/repositories/releases/" }
    }
}

ext.spongeApiVersion = "7.0.0"

configurations {
    compile.extendsFrom kapt
    compile.extendsFrom shadow
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.50"
    kapt "org.spongepowered:spongeapi:$spongeApiVersion"
    shadow "org.bstats:bstats-sponge:1.2"
    shadow("com.github.randombyte-developer.kosp:kosp:v2.2.2") { transitive = false }
    shadow(project("holograms-api")) { transitive = false }
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.holograms.shaded.kotlin"
    relocate "org.bstats", "de.randombyte.holograms.shaded.bstats"
    relocate "de.randombyte.kosp", "de.randombyte.holograms.shaded.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar
if (!isJitpack) {
    signArchives.dependsOn shadowJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}